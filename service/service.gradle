def version = '1.0.0'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:1.0.1"
    }
}

apply plugin: 'checkstyle' // http://www.gradle.org/docs/current/dsl/org.gradle.api.plugins.quality.CheckstyleExtension.html
apply plugin: 'cobertura'  // https://github.com/stevesaliman/gradle-cobertura-plugin
apply plugin: 'findbugs'   // http://www.gradle.org/docs/current/dsl/org.gradle.api.plugins.quality.FindBugsExtension.html
apply plugin: 'java'       // http://www.gradle.org/docs/current/userguide/java_plugin.html
apply plugin: 'jdepend'    // http://gradle.org/docs/current/dsl/org.gradle.api.plugins.quality.JDependExtension.html
apply plugin: 'maven'      // http://www.gradle.org/docs/current/userguide/maven_plugin.html
apply plugin: 'war'        // http://www.gradle.org/docs/current/userguide/war_plugin.html

idea.module {
    // add test source directories for the integrationTest source set
    testSourceDirs += file('src/integration-test/java')
    testSourceDirs += file('src/integration-test/resources')

    downloadSources = true  // tells Ivy to download sources jar files as well
}

repositories {
//    mavenLocal()  // This option fetches dependencies from your local .m2 directory if present, but it spams stdout
    mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')

        // integration tests can use classes and resources from main and test sourceSet
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

configurations {
    all {
        exclude(group: 'commons-logging') // Never include commons-logging (use SLF4J instead)
    }

    // use these configurations to specify dependencies only used by integration tests (and not by unit tests)
    integrationTestCompile { extendsFrom testCompile }
    integrationTestRuntime { extendsFrom integrationTestCompile, testRuntime }
}

jar.manifest {
    attributes("Implementation-Title": "Learn AngularJS", "Implementation-Version": version)
}

// Add to the end of the existing processResources task from the Gradle Java plugin
// http://gradle.org/docs/current/userguide/tutorial_using_tasks.html#N102B2
processResources << {
    // Workaround for Tomcat. Tomcat requires META-INF to be a non-empty subdirectory of
    // build/classes/main in order for Servlet 3.0 Application Initializer scanning to work
    copy {
        // http://gradle.org/docs/current/userguide/working_with_files.html
        from "src/main/resources/META-INF/dummy-file-required-by-tomcat.txt"
        into "${sourceSets.main.output.classesDir}/META-INF"
    }
}

dependencies {
    def cglibVersion = '2.2.2'
    def commonsLangVersion = '2.6'
    def guavaVersion = '13.0.1'
    def jacksonVersion = '2.0.6'
    def jodaTimeVersion = '2.1'
    def jsr250ApiVersion = '1.0'
    def slf4jVersion = '1.7.0'
    def springFrameworkVersion = '3.1.2.RELEASE'
    def springSecurityVersion = '3.1.2.RELEASE'

    def commonsCliVersion = '1.2'
    def hamcrestVersion = '1.3'
    def junitVersion = '4.10'
    def mockitoVersion = '1.9.0'

    def tomcatVersion = '7.0.30'

    compile "org.springframework:spring-webmvc:${springFrameworkVersion}",
            "cglib:cglib:${cglibVersion}",
            "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
            "org.springframework.security:spring-security-config:${springSecurityVersion}",
            "org.springframework.security:spring-security-web:${springSecurityVersion}",
            "com.google.guava:guava:${guavaVersion}",
            "commons-lang:commons-lang:${commonsLangVersion}",
            "joda-time:joda-time:${jodaTimeVersion}",
            "org.slf4j:slf4j-simple:${slf4jVersion}",
            "org.slf4j:slf4j-api:${slf4jVersion}",
            "org.slf4j:log4j-over-slf4j:${slf4jVersion}",
            "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    testCompile "junit:junit:${junitVersion}",
                "org.hamcrest:hamcrest-core:${hamcrestVersion}",
                "org.hamcrest:hamcrest-library:${hamcrestVersion}",
                "org.mockito:mockito-core:${mockitoVersion}",
                "org.springframework:spring-test:${springFrameworkVersion}",
                "commons-cli:commons-cli:${commonsCliVersion}"

    providedCompile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
                    "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
                    "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
}

task integrationTest(type: Test,
                     dependsOn: testClasses,
                     description: 'Runs the integration tests.',
                     group: 'verification') {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

task runServiceDaemon(dependsOn: testClasses,
                      description: 'Runs the service as a daemon for scenario tests in automated build.',
                      group: 'application') << {
    logging.setLevel(LogLevel.INFO)
    def serverTimeoutMillis = 300000
    ant.parallel(timeout: serverTimeoutMillis,
                 failonany: true) {
        ant.daemons {
            ant.java(classname: 'com.thoughtworks.learnangularjs.server.TomcatServer',
                     classpath: sourceSets.test.runtimeClasspath.asPath,
                     fork: true,
                     failonerror: true,
                     timeout: serverTimeoutMillis)
        }
        ant.waitfor(maxwait: 1, maxwaitunit: 'minute', checkevery: 100, checkeveryunit: 'millisecond') {
            ant.http(url: 'http://127.0.0.1:8000/app/index.html')
        }
    }
}

task runService(dependsOn: testClasses,
                description: 'Starts up the service using Tomcat.',
                group: 'application') << {
    logging.setLevel(LogLevel.INFO)
    ant.java(classname: 'com.thoughtworks.learnangularjs.server.TomcatServer',
             classpath: sourceSets.test.runtimeClasspath.asPath,
             fork: true,
             failonerror: true)
}
