apply plugin: 'war'

idea.module {
    downloadSources = true
}

repositories {
    mavenCentral()
}

configurations {
    // Never include commons-logging (use SLF4J instead)
    all {
        exclude(group: 'commons-logging')
    }

    // Stop IntelliJ IDEA from complaining about missing Ivy classes
    gradleIdea
}

dependencies {
    def cglibVersion = '2.2.2'
    def commonsLangVersion = '2.6'
    def guavaVersion = '13.0.1'
    def jacksonVersion = '1.9.9'
    def jodaTimeVersion = '2.1'
    def slf4jVersion = '1.7.0'
    def springFrameworkVersion = '3.1.2.RELEASE'
    def springSecurityVersion = '3.1.2.RELEASE'

    def hamcrestVersion = '1.3'
    def junitVersion = '4.10'

    def servletApiVersion = '3.0.1'
    def tomcatVersion = '7.0.30'

    compile "org.springframework:spring-webmvc:${springFrameworkVersion}",
            "cglib:cglib:${cglibVersion}",
            "org.codehaus.jackson:jackson-mapper-asl:${jacksonVersion}",
            "org.springframework.security:spring-security-config:${springSecurityVersion}",
            "org.springframework.security:spring-security-web:${springSecurityVersion}",
            "com.google.guava:guava:${guavaVersion}",
            "commons-lang:commons-lang:${commonsLangVersion}",
            "joda-time:joda-time:${jodaTimeVersion}",
            "org.slf4j:slf4j-simple:${slf4jVersion}",
            "org.slf4j:slf4j-api:${slf4jVersion}",
            "org.slf4j:log4j-over-slf4j:${slf4jVersion}",
            "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    testCompile "junit:junit:${junitVersion}",
            "org.hamcrest:hamcrest-core:${hamcrestVersion}",
            "org.hamcrest:hamcrest-library:${hamcrestVersion}",
            "org.springframework:spring-test:${springFrameworkVersion}"

    providedCompile "javax.servlet:javax.servlet-api:${servletApiVersion}",
            "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-log4j:${tomcatVersion}"

    gradleIdea "org.apache.ivy:ivy:2.2.0"
}

task startserver(dependsOn: testClasses) << {
    logging.setLevel(LogLevel.INFO)
    def serverTimeoutMillis = 300000
    ant.parallel(timeout: serverTimeoutMillis, failonany: true) {
        ant.daemons {
            ant.java(classname: 'com.thoughtworks.learnangularjs.server.TomcatServer',
                    classpath: sourceSets.test.runtimeClasspath.asPath,
                    fork: true,
                    failonerror: true,
                    timeout: serverTimeoutMillis) {
            }
        }
        ant.waitfor(maxwait: 1, maxwaitunit: 'minute', checkevery: 100, checkeveryunit: 'millisecond') {
            ant.http(url: 'http://127.0.0.1:8000/app/index.html')
        }
    }
}

